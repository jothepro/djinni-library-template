plugins {
    id 'com.android.library'
    id 'com.kezong.fat-aar'
    id 'maven-publish'
}


version = new File('../../../../VERSION').text

android {
    compileSdk 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdk 28
        targetSdk 30
        versionCode version.replaceAll("[.]", "").toInteger()
        versionName version
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    packagingOptions{
        doNotStrip "*/armeabi-v7a/*.so"
        doNotStrip "*/arm64-v8a/*.so"
        doNotStrip "*/x86/*.so"
        doNotStrip "*/x86_64/*.so"
    }
}

dependencies {
    embed name: 'MyDjinniLibrary'
    implementation name: 'MyDjinniLibrary'
}

publishing {
    publications {
        myPulication(MavenPublication) {
            groupId 'my.djinnilibrary'
            version version
            artifactId 'MyDjinniLibrary'
            artifact("$buildDir/outputs/aar/MyDjinniLibrary-release.aar")
        }
    }
    repositories {
        maven {
            credentials {
                username System.getenv("MAVEN_USERNAME")
                password System.getenv("MAVEN_PASSWORD")
            }
            url System.getenv("MAVEN_REPOSITORY")
        }
    }
}